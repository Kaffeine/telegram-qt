# TelegramQt@QT_VERSION_MAJOR@Config.cmake is generated by CMake from TelegramQtConfig.cmake.in.
# Any changed value in this file will be overwritten by CMake.

if(CMAKE_VERSION VERSION_LESS 3.6)
    message(FATAL_ERROR "TelegramQt requires at least CMake version 3.6")
endif()

# Valid modules: Core, Qml
set(_WANTED_COMPONENTS "${TelegramQt@QT_VERSION_MAJOR@_FIND_COMPONENTS}")
list (FIND _WANTED_COMPONENTS "Core" _CORE_INDEX)
if (${_CORE_INDEX} LESS 0)
    list(APPEND _WANTED_COMPONENTS Core)
endif()

# Find dependencies
include(CMakeFindDependencyMacro)

# Base Qt components set
set(_QT_COMPONENTS Core Network)

# Look for extra Qt modules wanted by some TelegramQt components
foreach(module ${_WANTED_COMPONENTS})
    if(${module} STREQUAL "Qml")
        list(APPEND QT_COMPONENTS Qml)
    endif()
endforeach()

find_dependency(Qt5 @QT_MINUMUM_VERSION@ COMPONENTS ${_QT_COMPONENTS})
unset(_QT_COMPONENTS)

# Finally include TelegramQt targets
foreach(module ${_WANTED_COMPONENTS})
    set(module_target "${CMAKE_CURRENT_LIST_DIR}/TelegramQt5${module}Targets.cmake")
    if(EXISTS "${module_target}")
        include("${module_target}")
    endif()

    if(NOT TARGET TelegramQt@QT_VERSION_MAJOR@::${module})
        if (TelegramQt@QT_VERSION_MAJOR@_FIND_REQUIRED_${module})
            set(_TelegramQt@QT_VERSION_MAJOR@_NOTFOUND_MESSAGE
                "${_TelegramQt@QT_VERSION_MAJOR@_NOTFOUND_MESSAGE}Failed to find TelegramQt@QT_VERSION_MAJOR@ component \"${module}\"\n")
        elseif(NOT TelegramQt@QT_VERSION_MAJOR@_FIND_QUIETLY)
            message(WARNING "Failed to find TelegramQt@QT_VERSION_MAJOR@ component \"${module}\"")
        endif()
    endif()
endforeach()
unset(_WANTED_COMPONENTS)

if (_TelegramQt@QT_VERSION_MAJOR@_NOTFOUND_MESSAGE)
    set(TelegramQt@QT_VERSION_MAJOR@_NOT_FOUND_MESSAGE "${_TelegramQt@QT_VERSION_MAJOR@_NOTFOUND_MESSAGE}")
    set(TelegramQt@QT_VERSION_MAJOR@_FOUND False)
endif()

# The code below is needed only for backward compatibility and will be removed from the release

# Setup the LIBRARIES variable
get_target_property(TELEGRAM_QT@QT_VERSION_MAJOR@_LIBRARIES
    TelegramQt@QT_VERSION_MAJOR@::Core
    IMPORTED_LOCATION
)

if(NOT TELEGRAM_QT@QT_VERSION_MAJOR@_LIBRARIES)
    # If there is no default IMPORTED_LOCATION then get all configurations...
    get_target_property(_IMPORTED_CONFIGURATIONS
        TelegramQt@QT_VERSION_MAJOR@::Core
        IMPORTED_CONFIGURATIONS
    )
    # ... and pick up the first one
    list(GET _IMPORTED_CONFIGURATIONS 0 _SELECTED_CONFIGURATION)
    get_target_property(TELEGRAM_QT@QT_VERSION_MAJOR@_LIBRARIES
        TelegramQt@QT_VERSION_MAJOR@::Core
        IMPORTED_LOCATION_${_SELECTED_CONFIGURATION}
    )
    unset(_IMPORTED_CONFIGURATIONS)
    unset(_SELECTED_CONFIGURATION)
endif()

# Setup the INCLUDE_DIR variable
get_target_property(TELEGRAM_QT@QT_VERSION_MAJOR@_INCLUDE_DIR
    TelegramQt@QT_VERSION_MAJOR@::Core
    INTERFACE_INCLUDE_DIRECTORIES
)
